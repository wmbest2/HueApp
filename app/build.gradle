buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:0.11.91'
        classpath 'org.jetbrains.kotlin:kotlin-android-extensions:0.11.91'
        classpath 'com.wmbest.gradle:rats:0.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

def headCommitCount() {
    def cmd = "git rev-list HEAD --count"
    return cmd.execute().text.toInteger()
}

android {
    compileSdkVersion 22
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId 'com.wmbest.hue'
        minSdkVersion 16
        targetSdkVersion 21 
        versionCode headCommitCount() //Auto-increment the version based on how many commits have occurred.
        versionName "1.0.0 (" + versionCode + ")"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        disable 'InvalidPackage'
        enable 'IconExpectedSize', 'SelectableText', 'UnusedIds', 'StopShip', 'GradleDynamicVersion'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries false
    }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:0.11.91'

    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:palette-v7:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    
    compile 'com.github.rey5137:material:1.0.0'

    compile ('io.realm:realm-android:0.80.0') {
        exclude group: 'com.squareup.javawriter'
    } 
    compile "com.google.android.gms:play-services:6.5.87"

    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'

    compile 'com.jakewharton:butterknife:5.1.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.picasso:picasso:2.3.3'

    compile 'io.reactivex:rxandroid:0.24.0'

    //Testing
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support'
    }
}
